buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.12.1"
    id "org.openjfx.javafxplugin" version "0.0.7"
}

apply plugin: 'application'

version = '0.17.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'license'

    sourceCompatibility = 11
    targetCompatibility = 11

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    license {
        include "**/*.java"
        header project.file('LICENSE.header')
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }

    javadoc {
        options.addBooleanOption('html5', true)
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
    }
}

dependencies {
    compile project(':jskat-base')
    compile project(':jskat-swing-gui')
    compile project(':jskat-javafx-gui')
}

startScripts {
    mainClassName 'org.jskat.JSkatFX'
    applicationDefaultJvmArgs = ["-Djdk.gtk.version=2"]
}

mainClassName = 'org.jskat.JSkatFX'

jar {
    manifest {
        attributes 'Main-Class': 'org.jskat.Launcher'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
